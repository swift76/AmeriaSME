if exists (select * from sys.objects where name='sp_SaveNORQQueryResult' and type='P')
	drop procedure sp_SaveNORQQueryResult
GO

create procedure sp_SaveNORQQueryResult(
	@APPLICATION_ID					uniqueidentifier,
	@FIRST_NAME						nvarchar(20),
	@LAST_NAME						nvarchar(20),
	@PATRONYMIC_NAME				nvarchar(20),
	@BIRTH_DATE						date,
	@GENDER							bit,
	@DISTRICT						nvarchar(20),
	@COMMUNITY						nvarchar(40),
	@STREET							nvarchar(100),
	@BUILDING						nvarchar(40),
	@APARTMENT						nvarchar(40),
	@ADDRESS						nvarchar(100),
	@FEE							money,
	@PASSPORT_NUMBER				char(9),
	@PASSPORT_DATE					date,
	@PASSPORT_EXPIRY_DATE			date,
	@PASSPORT_BY					char(3),
	@BIOMETRIC_PASSPORT_NUMBER		char(9),
	@BIOMETRIC_PASSPORT_ISSUE_DATE	date,
	@BIOMETRIC_PASSPORT_EXPIRY_DATE	date,
	@BIOMETRIC_PASSPORT_ISSUED_BY	char(3),
	@ID_CARD_NUMBER					char(9),
	@ID_CARD_ISSUE_DATE				date,
	@ID_CARD_EXPIRY_DATE			date,
	@ID_CARD_ISSUED_BY				char(3),
	@SOCIAL_CARD_NUMBER				char(10),
	@RESPONSE_XML					nvarchar(max),
	@WORK_DATA						NORQQueryResultWork	readonly
)
AS
	BEGIN TRANSACTION
	BEGIN TRY
		declare @STATUS_ID tinyint

		select @STATUS_ID = STATUS_ID from APPLICATION with (updlock) where ID = @APPLICATION_ID

		if @STATUS_ID <> 4
			RAISERROR (N'Հայտի կարգավիճակն արդեն փոփոխվել է', 17, 1)

		insert into NORQ_QUERY_RESULT
			(APPLICATION_ID,FIRST_NAME,LAST_NAME,PATRONYMIC_NAME,BIRTH_DATE,GENDER,
			DISTRICT,COMMUNITY,STREET,BUILDING,APARTMENT,ADDRESS,
			PASSPORT_NUMBER,PASSPORT_DATE,PASSPORT_EXPIRY_DATE,PASSPORT_BY,
			BIOMETRIC_PASSPORT_NUMBER,BIOMETRIC_PASSPORT_ISSUE_DATE,BIOMETRIC_PASSPORT_EXPIRY_DATE,BIOMETRIC_PASSPORT_ISSUED_BY,
			ID_CARD_NUMBER,ID_CARD_ISSUE_DATE,ID_CARD_EXPIRY_DATE,ID_CARD_ISSUED_BY,
			SOCIAL_CARD_NUMBER,FEE,RESPONSE_XML)
		values
			(@APPLICATION_ID,@FIRST_NAME,@LAST_NAME,@PATRONYMIC_NAME,@BIRTH_DATE,@GENDER,
			@DISTRICT,@COMMUNITY,@STREET,@BUILDING,@APARTMENT,@ADDRESS,
			@PASSPORT_NUMBER,@PASSPORT_DATE,@PASSPORT_EXPIRY_DATE,@PASSPORT_BY,
			@BIOMETRIC_PASSPORT_NUMBER,@BIOMETRIC_PASSPORT_ISSUE_DATE,@BIOMETRIC_PASSPORT_EXPIRY_DATE,@BIOMETRIC_PASSPORT_ISSUED_BY,
			@ID_CARD_NUMBER,@ID_CARD_ISSUE_DATE,@ID_CARD_EXPIRY_DATE,@ID_CARD_ISSUED_BY,
			@SOCIAL_CARD_NUMBER,@FEE,@RESPONSE_XML)

		delete from NORQ_QUERY_RESULT_WORK where APPLICATION_ID=@APPLICATION_ID
		insert into NORQ_QUERY_RESULT_WORK
			(APPLICATION_ID,ORGANIZATION_NAME,TAX_ID_NUMBER,ADDRESS,FROM_DATE,TO_DATE,SALARY,SOCIAL_PAYMENT,POSITION)
		select @APPLICATION_ID,ORGANIZATION_NAME,TAX_ID_NUMBER,ADDRESS,FROM_DATE,TO_DATE,SALARY,SOCIAL_PAYMENT,POSITION
			from @WORK_DATA

		execute sp_ChangeApplicationStatus @APPLICATION_ID,5
	END TRY
	BEGIN CATCH
		ROLLBACK TRANSACTION
		declare @ErrorMessage varchar(4000)
		set @ErrorMessage = ERROR_MESSAGE()
		RAISERROR (@ErrorMessage, 17, 1)
		RETURN
	END CATCH
	COMMIT TRANSACTION
GO
